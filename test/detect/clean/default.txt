module fsieve

/*
The Fast Sieve of Eratosthenes.

A sequential and optimized version of the sieve of Eratosthenes.
The program calculates a list of the first NrOfPrime primes.
The result of the program is the NrOfPrimes'th prime.

Strictness annotations have been added because the strictness analyser
is not able to deduce all strictness information. Removal of these !'s
will make the program about 20% slower.

On a machine without a math coprocessor the execution of this
program might take a (very) long time. Set NrOfPrimes to a smaller value.
*/

import StdClass; // RWS
import StdInt, StdReal

NrOfPrimes :== 3000

// The sieve algorithm: generate an infinite list of all primes.

primes :: [Int]
primes = pr where pr = [5 : sieve 7 4 pr]

sieve :: Int !Int [Int] -> [Int]
sieve g i prs
| isPrime prs g (toInt (sqrt (toReal g))) = [g : sieve` g i prs]
| otherwise                               = sieve (g + i) (6 - i) prs

sieve` :: Int Int [Int] -> [Int]
sieve` g i prs = sieve (g + i) (6 - i) prs

isPrime :: [Int] !Int Int -> Bool
isPrime [f:r] pr bd
| f>bd        =  True
| pr rem f==0 =  False
| otherwise   =  isPrime r pr bd

// select is used to get the NrOfPrimes'th prime from the infinite list.

select :: [x] Int -> x
select [f:r] 1 = f
select [f:r] n = select r (n - 1)

/* The Start rule: Select the NrOfPrimes'th prime from the list of primes
 * generated by Primes.
 */

Start :: Int
Start = select [2, 3 : primes] NrOfPrimes
